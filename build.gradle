plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'pl.allegro.tech.build.axion-release' version '1.13.2'
}

group = 'com.github.nickmcdowall'
version = scmVersion.version

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    
    implementation 'org.projectlombok:lombok:1.18.16'
    implementation 'io.pebbletemplates:pebble:3.1.4'
    implementation "net.sourceforge.plantuml:plantuml:1.2021.7"
    implementation "org.jdom:jdom:1.1"
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.j2html:j2html:1.4.0'

    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation("com.approvaltests:approvaltests:11.4.0")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "$group"
            artifactId = 'lsd-core'
            version = "${scmVersion.version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'lsd-core'
                description = 'A library used to generate living sequence diagrams'
                url = 'https://github.com/nickmcdowall/lsd-core.git'
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://raw.githubusercontent.com/nickmcdowall/lsd-core/main/LICENSE.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Nick"
                        email = "nicholas.mcdowall@gmail.com"
                        organization = 'NKM IT Solutions'
                        organizationUrl = 'https://github.com/nickmcdowall'
                    }
                }
                scm {
                    url = "https://github.com/nickmcdowall/lsd-core.git"
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials(PasswordCredentials)
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
